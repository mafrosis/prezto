#
# Exposes information about the Python environment via the $python_info
# associative array.
#
# Authors:
#   Sorin Ionescu <sorin.ionescu@gmail.com>
#   Patrick Bos <egpbos@gmail.com>
#

local virtualenv_format
local virtualenv_formatted
local version_format
local version_formatted
local version

# Clean up previous $python_info.
unset python_info
typeset -gA python_info

# Grab the styling we might have to do
zstyle -s ':prezto:module:python:info:virtualenv' format 'virtualenv_format'
zstyle -s ':prezto:module:python:info:version' format 'version_format'

# Format virtualenv.
if [[ -n "$virtualenv_format" ]]; then
  # Handle Hatch project envs
  if [[ -n "$HATCH_ENV_ACTIVE" ]]; then
    zstyle -s ':prezto:module:python:info:hatchenv' format 'virtualenv_format'
    zformat -f virtualenv_formatted "$virtualenv_format" "v:${HATCH_ENV_ACTIVE}"
    python_info[virtualenv]="$virtualenv_formatted"

  # Handle Python virtualenv
  elif [[ -n "$VIRTUAL_ENV" ]]; then
    zstyle -s ':prezto:module:python:info:virtualenv' format 'virtualenv_format'
    zformat -f virtualenv_formatted "$virtualenv_format" "v:${VIRTUAL_ENV:h:t}"
    python_info[virtualenv]="$virtualenv_formatted"

  # Handle Conda virtual environments
  elif [[ -n "$CONDA_DEFAULT_ENV" ]]; then
    zformat -f virtualenv_formatted "$virtualenv_format" "v:${CONDA_DEFAULT_ENV:t}"
    python_info[virtualenv]="$virtualenv_formatted"
  fi
fi
